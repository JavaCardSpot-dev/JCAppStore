//import org.gradle.internal.os.OperatingSystem;

plugins {
    id 'java'
    id 'com.bmuschko.izpack' version '3.0'
}

group 'cz.muni.crocs.appletstore'
version '1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()

    maven {
        // distribution repository for izpack
        name "Ice Maven Release Repository"
        url "http://anonsvn.icesoft.org/repo/maven2/releases"
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1'

    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.11.1'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.2'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'

    compile group: 'com.github.martinpaljak', name: 'globalplatformpro', version: '19.06.16'
    compile group: 'com.github.martinpaljak', name: 'apdu4j-core', version: '19.05.08'
    compile group: 'com.github.martinpaljak', name: 'apdu4j-pcsc', version: '19.05.08'

    compile group: 'com.miglayout', name: 'miglayout', version: '3.7.4'
    compile group: 'org.ini4j', name: 'ini4j', version: '0.5.4'
    compile group: 'org.simplejavamail', name: 'simple-java-mail', version: '4.1.2'

    izpack 'org.codehaus.izpack:izpack-dist:5.1.3'
}

///////////////////////////
//windows installer section
///////////////////////////

izpack {
    //generate folders deploy, out, res, main
    def folder = file("$buildDir/deploy-win/out/src/main/resources")
    if( !folder.exists() ) {
        folder.mkdirs()
    }

    def scripts = file("$buildDir/deploy-win/scripts")
    if( !scripts.exists() ) {
        scripts.mkdirs()
    }

    copy {
        from "src/main/resources"
        into "$buildDir/deploy-win/out/src/main/resources"
    }

    copy {
        from "installer-win/dependency"
        into "$buildDir/deploy-win/scripts"
    }

    //todo somehow the jar dependency does not generate it quickly enough so the code is working for the 2nd time :(
    //maybe add this into jar file
    copy {
        from file("build/libs/JCAppStore-${version}.jar")
        into file("$buildDir/deploy-win/out")
    }

    baseDir = file("$buildDir/deploy-win")
    installFile = file('installer-win/installer.xml')
    outputFile = file("$buildDir/deploy-win/JCAppStore-${version}-win.jar")
    compression = 'deflate'
    compressionLevel = 9
    appProperties = ['app.group': 'CRoCS', 'app.name': 'jcAppStore', 'app.title': 'JCAppStore',
                     'app.version': version, 'app.subpath': "JCAppStore-$version"]
}

izPackCreateInstaller.dependsOn jar

///////////////////////////
//unix installer section
///////////////////////////

task unix(type: Exec) {
    group 'installation'
    executable "$projectDir/installer-unix/installer.sh"
}

unix.dependsOn jar

///////////////////////////
//binary section
///////////////////////////
jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
    manifest {
        attributes 'Main-Class': 'cz.muni.crocs.appletstore.SplashScreen'
    }
}




